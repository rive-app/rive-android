apply plugin: "io.github.gradle-nexus.publish-plugin"
apply plugin: "com.palantir.git-version"

def details = versionDetails()

ext {
    PUBLISH_GROUP_ID = "app.rive"
    PUBLISH_VERSION = details.lastTag
    PUBLISH_ARTIFACT_ID = "rive-android"
}
buildscript {
    ext {
        gradle_version = "8.10.1"
        // Sticking with Kotlin 1.9.x for now for maximum compatibility.
        // Not all projects use 2.x+ yet, and forcing an upgrade can increase client DEX sizes.
        kotlin_version = "1.9.25"
        dokkaVersion = "1.4.30"
        // See the table here for matching Kotlin version with Compose Compiler version
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin#pre-release_kotlin_compatibility
        compose_compiler_version = "1.5.15"
        // The last Compose BOM version that supports Kotlin 1.9.x
        compose_bom_version = "2023.10.00"
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:$gradle_version")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath("androidx.compose.compiler:compiler:$compose_compiler_version")
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}")
        classpath("io.github.gradle-nexus:publish-plugin:2.0.0")
        classpath("com.palantir.gradle.gitversion:gradle-git-version:3.1.0")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

tasks.register("cleanDeps", Exec) {
    workingDir("./kotlin/src/main/cpp")
    commandLine("./clean_all.sh")
}

tasks.register("clean", Delete) {
    dependsOn(cleanDeps)
    delete(rootProject.buildDir)
}

apply from: "${rootDir}/scripts/publish-root.gradle"
