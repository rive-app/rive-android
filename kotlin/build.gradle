plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.dokka)
    alias(libs.plugins.allopen)
    alias(libs.plugins.git.version)
    alias(libs.plugins.maven.publish)
}

android {
    compileSdk = 36
    ndkVersion = "27.2.12479018"
    namespace = "app.rive.runtime.kotlin"

    buildFeatures {
        compose = true
    }

    composeOptions {
        // Chosen to match Kotlin version
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion = libs.versions.composeCompiler.get()
    }

    // Enable removing miniaudio to reduce binary size
    def audioEnabled = !project.hasProperty("noAudio")
    // Enable ASAN support when debugging
    def asanEnabled = project.hasProperty("asan")

    defaultConfig {
        minSdkVersion 21
        targetSdk = 36

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
        externalNativeBuild {
            cmake {
                // Used during PR matrix builds to build each architecture as a separate job
                // Also useful for local testing
                if (project.hasProperty("abiFilters")) {
                    // Take a comma-separated string from the project property,
                    // split it into a list, and trim whitespace from each item
                    def abiList = project.property('abiFilters').split(',').collect { it.trim() }
                    // Spread (*) the list elements as separate arguments
                    abiFilters(*abiList)
                } else {
                    // Default to building all when no property is passed
                    abiFilters "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
                }
                arguments "-DCMAKE_VERBOSE_MAKEFILE=1", "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=ON",
                        "-DANDROID_CPP_FEATURES=no-exceptions no-rtti", "-DANDROID_STL=c++_shared",
                        // Support for 16kb page sizes, necessary for NDK r27
                        // Can remove when upgrading to r28+
                        // https://developer.android.com/guide/practices/page-sizes#compile-r27
                        "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON",
                        // Toggle miniaudio
                        "-DWITH_RIVE_AUDIO=${audioEnabled ? 'ON' : 'OFF'}",
                        // Needed for ASAN support (if enabled)
                        // See https://developer.android.com/ndk/guides/asan#building
                        // and https://developer.android.com/ndk/guides/cmake#android_arm_mode
                        "-DANDROID_ARM_MODE=${asanEnabled ? 'arm' : 'thumb'}",
                        // Enable ASAN if requested
                        "-DENABLE_ASAN=${asanEnabled ? 'ON' : 'OFF'}"
            }
        }
    }

    def javaVersion = JavaVersion.VERSION_11
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
            version = "3.22.1"
        }
    }

    dokkaGfm {
        dokkaSourceSets {
            named("main") {
                noAndroidSdkLink = false
                outputDirectory = layout.buildDirectory.resolve("dokka")
                reportUndocumented = true
            }
        }
    }
}

dependencies {
    def composeBom = platform(libs.androidx.compose.bom)

    implementation(composeBom)
    implementation(libs.android.volley)
    implementation(libs.androidx.compose.runtime)
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.android)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.compose)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.startup.runtime)
    // Required until minSdk >= 23
    // https://github.com/KeepSafe/ReLinker?tab=readme-ov-file#who-needs-relinker
    implementation(libs.relinker)

    // Used by Compose previews and UI tests
    debugImplementation(composeBom)
    debugImplementation(libs.androidx.compose.ui.test.manifest)
    // Used to debug Compose recompositions
    debugImplementation(libs.rebugger)

    androidTestImplementation(composeBom)
    androidTestImplementation(libs.androidx.compose.foundation.layout)
    androidTestImplementation(libs.androidx.compose.material3)
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    androidTestImplementation(libs.androidx.test.ext.junit.ktx)
    androidTestImplementation(libs.androidx.test.runner)
    androidTestImplementation(libs.kotlin.test.junit)
    androidTestImplementation(libs.kotlinx.coroutines.test)
}

allOpen {
    // Allows mocking for classes without opening them for release builds
    annotation("androidx.annotation.OpenForTesting")
}

// Clean up native build and generated files
tasks.named('clean', Delete) {
    delete(
            layout.projectDirectory.dir('.cxx'),
            layout.projectDirectory.dir('src/main/cpp/dependencies'),
            layout.projectDirectory.dir('src/main/cpp/out'),
    )
}

def details = versionDetails()
def PUBLISH_GROUP_ID = "app.rive"
def PUBLISH_VERSION = details.lastTag
def PUBLISH_ARTIFACT_ID = "rive-android"

mavenPublishing {
    // `true` will automatically publish the artifact to Maven Central Portal.
    publishToMavenCentral(true)
    signAllPublications()
    coordinates(PUBLISH_GROUP_ID, PUBLISH_ARTIFACT_ID, PUBLISH_VERSION)

    // Mostly self-explanatory metadata
    pom {
        name = PUBLISH_ARTIFACT_ID
        description = 'Rive is a real-time interactive design and animation tool. Use our collaborative editor to create motion graphics that respond to different states and user inputs. Then load your animations into apps, games, and websites with our lightweight open-source runtimes.'
        url = 'https://rive.app'
        licenses {
            license {
                name = 'MIT License'
                url = 'https://github.com/rive-app/rive-android/blob/master/LICENSE'
            }
        }

        developers {
            developer {
                id = 'erikuggeldahl'
                name = 'Erik Uggeldahl'
                email = 'erik@rive.app'
                roles = ['Android DevRel']
            }
            developer {
                id = 'umberto-sonnino'
                name = 'Umberto Sonnino'
                email = 'umberto@rive.app'
                roles = ['Original Author']
            }
            developer {
                id = 'luigi-rosso'
                name = 'Luigi Rosso'
                email = 'luigi@rive.app'
                roles = ['Founder', 'CTO']
            }
            developer {
                id = 'mjtalbot'
                name = 'Maxwell Talbot'
                roles = ['Original Contributor']
            }
        }

        // Version control info
        scm {
            connection = 'scm:git:git@github.com:rive-app/rive-android.git'
            developerConnection = 'scm:git:ssh://git@github.com:rive-app/rive-android.git'
            url = 'https://github.com/rive-app/rive-android/'
        }
    }
}
