plugins {
    id "com.android.library"
    id "kotlin-android"
    id "org.jetbrains.dokka"
    id "org.jetbrains.kotlin.plugin.allopen" version "2.1.20"
}

android {
    compileSdk 35
    ndkVersion "27.2.12479018"
    namespace "app.rive.runtime.kotlin"

    defaultConfig {
        minSdkVersion 21
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
                arguments "-DCMAKE_VERBOSE_MAKEFILE=1", "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=ON",
                        "-DANDROID_CPP_FEATURES=no-exceptions no-rtti", "-DANDROID_STL=c++_shared",
                        // Support for 16kb page sizes, necessary for NDK r27
                        // Can remove when upgrading to r28+
                        // https://developer.android.com/guide/practices/page-sizes#compile-r27
                        "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
            }
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    externalNativeBuild {
        cmake {
            path file("src/main/cpp/CMakeLists.txt")
            version "3.22.1"
        }
    }

    dokkaGfm {
        dokkaSourceSets {
            named("main") {
                noAndroidSdkLink.set(false)
                outputDirectory.set(layout.buildDirectory.resolve("dokka"))
                reportUndocumented.set(true)
            }
        }
    }
}

dependencies {
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.7"
    implementation "androidx.startup:startup-runtime:1.2.0"
    implementation "com.android.volley:volley:1.2.1"
    // Required until minSdk >= 23
    // https://github.com/KeepSafe/ReLinker?tab=readme-ov-file#who-needs-relinker
    implementation "com.getkeepsafe.relinker:relinker:1.4.5"

    androidTestImplementation "androidx.test.ext:junit-ktx:1.2.1"
    androidTestImplementation "androidx.test:runner:1.6.2"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:2.1.0"
}

allOpen {
    // Allows mocking for classes without opening them for release builds
    annotation "androidx.annotation.OpenForTesting"
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
