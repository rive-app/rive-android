plugins {
    id "com.android.library"
    id "kotlin-android"
    id "org.jetbrains.dokka"
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlin_version"
    id "com.vanniktech.maven.publish"
}

android {
    compileSdk = 36
    ndkVersion = "27.2.12479018"
    namespace = "app.rive.runtime.kotlin"

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = compose_compiler_version
    }

    // Enable removing miniaudio to reduce binary size
    def audioEnabled = !project.hasProperty("noAudio")
    // Enable ASAN support when debugging
    def asanEnabled = project.hasProperty("asan")

    defaultConfig {
        minSdkVersion 21
        targetSdk = 36

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
        externalNativeBuild {
            cmake {
                // Used during PR matrix builds to build each architecture as a separate job
                // Also useful for local testing
                if (project.hasProperty("abiFilters")) {
                    // Take a comma-separated string from the project property,
                    // split it into a list, and trim whitespace from each item
                    def abiList = project.property('abiFilters').split(',').collect { it.trim() }
                    // Spread (*) the list elements as separate arguments
                    abiFilters(*abiList)
                } else {
                    // Default to building all when no property is passed
                    abiFilters "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
                }
                arguments "-DCMAKE_VERBOSE_MAKEFILE=1", "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=ON",
                        "-DANDROID_CPP_FEATURES=no-exceptions no-rtti", "-DANDROID_STL=c++_shared",
                        // Support for 16kb page sizes, necessary for NDK r27
                        // Can remove when upgrading to r28+
                        // https://developer.android.com/guide/practices/page-sizes#compile-r27
                        "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON",
                        // Toggle miniaudio
                        "-DWITH_RIVE_AUDIO=${audioEnabled ? 'ON' : 'OFF'}",
                        // Needed for ASAN support (if enabled)
                        // See https://developer.android.com/ndk/guides/asan#building
                        // and https://developer.android.com/ndk/guides/cmake#android_arm_mode
                        "-DANDROID_ARM_MODE=${asanEnabled ? 'arm' : 'thumb'}",
                        // Enable ASAN if requested
                        "-DENABLE_ASAN=${asanEnabled ? 'ON' : 'OFF'}"
            }
        }
    }

    def javaVersion = JavaVersion.VERSION_11
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
            version = "3.22.1"
        }
    }

    dokkaGfm {
        dokkaSourceSets {
            named("main") {
                noAndroidSdkLink = false
                outputDirectory = layout.buildDirectory.resolve("dokka")
                reportUndocumented = true
            }
        }
    }
}

dependencies {
    def composeBom = platform("androidx.compose:compose-bom:$compose_bom_version")

    implementation(composeBom)
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-android"
    implementation "androidx.core:core-ktx:1.17.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "androidx.startup:startup-runtime:1.2.0"
    implementation "com.android.volley:volley:1.2.1"
    // Required until minSdk >= 23
    // https://github.com/KeepSafe/ReLinker?tab=readme-ov-file#who-needs-relinker
    implementation "com.getkeepsafe.relinker:relinker:1.4.5"

    // Used by Compose previews and UI tests
    debugImplementation(composeBom)
    debugImplementation("androidx.compose.ui:ui-test-manifest")
    // Used to debug Compose recompositions
    debugImplementation "io.github.theapache64:rebugger:1.0.0-rc03"

    androidTestImplementation(composeBom)
    androidTestImplementation "androidx.compose.foundation:foundation-layout"
    androidTestImplementation "androidx.compose.material3:material3"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.3.0"
    androidTestImplementation "androidx.test:runner:1.7.0"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    //noinspection NewerVersionAvailable Last version that supports Kotlin 1.9.x
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1"
}

allOpen {
    // Allows mocking for classes without opening them for release builds
    annotation("androidx.annotation.OpenForTesting")
}

mavenPublishing {
    // `true` will automatically publish the artifact to Maven Central Portal.
    publishToMavenCentral(true)
    signAllPublications()
    coordinates(PUBLISH_GROUP_ID, PUBLISH_ARTIFACT_ID, PUBLISH_VERSION)

    // Mostly self-explanatory metadata
    pom {
        name = PUBLISH_ARTIFACT_ID
        description = 'Rive is a real-time interactive design and animation tool. Use our collaborative editor to create motion graphics that respond to different states and user inputs. Then load your animations into apps, games, and websites with our lightweight open-source runtimes.'
        url = 'https://rive.app'
        licenses {
            license {
                name = 'MIT License'
                url = 'https://github.com/rive-app/rive-android/blob/master/LICENSE'
            }
        }

        developers {
            developer {
                id = 'luigi-rosso'
                name = 'Luigi Rosso'
                email = 'luigi@rive.app'
            }
            developer {
                id = 'umberto-sonnino'
                name = 'Umberto Sonnino'
                email = 'umberto@rive.app'
            }
            developer {
                id = 'mjtalbot'
                name = 'Maxwell Talbot'
                email = 'max@rive.app'
            }
            developer {
                id = 'erikuggeldahl'
                name = 'Erik Uggeldahl'
                email = 'erik@rive.app'
            }
        }

        // Version control info
        scm {
            connection = 'scm:git:git@github.com:rive-app/rive-android.git'
            developerConnection = 'scm:git:ssh://git@github.com:rive-app/rive-android.git'
            url = 'https://github.com/rive-app/rive-android/tree/master'
        }
    }
}